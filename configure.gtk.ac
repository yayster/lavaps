
dnl -*-autoconf-*-
dnl configure.gtk.ac
dnl $Id: configure.gtk.ac,v 1.11 2004/12/25 02:34:34 johnh Exp $
dnl
dnl tcl/tk-specific part of configure.in
dnl

dnl
dnl pkg_check_version stolen from xscreensaver-4.09 by jwz
dnl
pkgs=''
pkg_check_version() {
  req="$1"
  min="$2"
  AC_MSG_CHECKING(for $req or later)
  if $PKG_CONFIG --exists "$req" ; then
    vers=`$PKG_CONFIG --modversion "$req"`
    if $PKG_CONFIG --exists "$req >= $min" ; then
      AC_MSG_RESULT($vers)
      pkgs="$pkgs $req"
      return 1
    else
      AC_MSG_RESULT($vers (wanted >= $min))
      ok=no
      return 0
    fi
  fi
}

pkg_check_version libgnomeui-2.0  2.2.0 && AC_MSG_ERROR([no libgnomeui-2.0])
pkg_check_version libgnomecanvas-2.0  2.2.0 && AC_MSG_ERROR([no libgnomecanvas-2.0])

AC_CACHE_CHECK([for Gtk includes], ac_cv_gtk_config_cflags,
                     [ac_cv_gtk_config_cflags=`$PKG_CONFIG --cflags $pkgs`])
AC_CACHE_CHECK([for Gtk libs], ac_cv_gtk_config_libs,
                     [ac_cv_gtk_config_libs=`$PKG_CONFIG --libs $pkgs`])


dnl AM_GCONF_SOURCE_2
dnl Defines GCONF_SCHEMA_CONFIG_SOURCE which is where you should install schemas
dnl  (i.e. pass to gconftool-2
dnl Defines GCONF_SCHEMA_FILE_DIR which is a filesystem directory where
dnl  you should install foo.schemas files
dnl

AC_DEFUN([AM_GCONF_SOURCE_2],
[
  if test "x$GCONF_SCHEMA_INSTALL_SOURCE" = "x"; then
    GCONF_SCHEMA_CONFIG_SOURCE=`gconftool-2 --get-default-source`
  else
    GCONF_SCHEMA_CONFIG_SOURCE=$GCONF_SCHEMA_INSTALL_SOURCE
  fi

  AC_ARG_WITH(gconf-source, 
  [  --with-gconf-source=sourceaddress      Config database for installing schema files.],GCONF_SCHEMA_CONFIG_SOURCE="$withval",)

  AC_SUBST(GCONF_SCHEMA_CONFIG_SOURCE)
  AC_MSG_RESULT([Using config source $GCONF_SCHEMA_CONFIG_SOURCE for schema installation])

  if test "x$GCONF_SCHEMA_FILE_DIR" = "x"; then
    GCONF_SCHEMA_FILE_DIR='$(sysconfdir)/gconf/schemas/'
  else
    GCONF_SCHEMA_FILE_DIR=$GCONF_SCHEMA_FILE_DIR
  fi

  AC_ARG_WITH(gconf-schema-file-dir, 
  [  --with-gconf-schema-file-dir=dir        Directory for installing schema files.],GCONF_SCHEMA_FILE_DIR="$withval",)

  AC_SUBST(GCONF_SCHEMA_FILE_DIR)
  AC_MSG_RESULT([Using $GCONF_SCHEMA_FILE_DIR as install directory for schema files])

  AC_ARG_ENABLE(schemas-install,
     [  --disable-schemas-install	Disable the schemas installation],
     [case "${enableval}" in
       yes) schemas_install=true ;;
       no)  schemas_install=false ;;
       *) AC_MSG_ERROR(bad value ${enableval} for --disable-schemas-install) ;;
     esac],[schemas_install=true])
     AM_CONDITIONAL(GCONF_SCHEMAS_INSTALL, test x$schemas_install = xtrue)
])

dnl
dnl gconf
dnl
AC_PATH_PROG(GCONFTOOL, gconftool-2)
AM_GCONF_SOURCE_2


GCONF_CONFIG_SOURCE=
AC_ARG_ENABLE(gconf-source, [  --enable-gconf-source=sourceaddress Where to install schema files.], GCONF_CONFIG_SOURCE=$enable_gconf_source,)
if test "x$GCONF_CONFIG_SOURCE" = "x"; then
        GCONF_CONFIG_SOURCE="xml::\${sysconfdir}/gconf/gconf.xml.defaults"
fi
AC_SUBST(GCONF_CONFIG_SOURCE)
AC_SUBST(INSTALL_GCONF_CONFIG_SOURCE)

dnl
dnl gettext
dnl

GETTEXT_PACKAGE=lavaps
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE",[gettext-package description])

AC_PROG_INTLTOOL

ALL_LINGUAS="es ru"
AM_GLIB_GNU_GETTEXT

dnl transition back into my code
GTK_INCLUDES="$ac_cv_gtk_config_cflags"
GTK_LIBS="$ac_cv_gtk_config_libs"
